wtf: BR* object EOF

object: (stmt BR+)*

stmt:
	( setPropertyStmt
	| defineStmt
	| applyStmt
	| ifStmt
	| forStmt
	| repeatStmt
	| debugStmt
	| expr ) ";"?

setPropertyStmt
	: ( expr BR* "." BR* )? IDENTIFIER ":" BR* expr
	| ( expr BR* "." BR* )? IDENTIFIER "(" param ")" ":" BR* stmt

defineStmt
	: "define" BR* IDENTIFIER BR* ":" BR* body
	| "define" BR* IDENTIFIER BR* "(" param ")" BR* ":" BR* expr

applyStmt: ":" BR* expr

ifStmt
	: "if" BR* "(" BR* expr BR* ")" BR*
	( body | body? BR* "else" BR* body)

forStmt
	: "for" BR* "(" BR* IDENTIFIER BR* "in" BR* expr BR* ")" BR* body

repeatStmt
	: "repeat" BR* "(" BR* expr BR* ")" BR* body

debugStmt: "debug" BR* expr

body
	: stmt
	| block

block: "{" BR* object "}"


expr: commaOperator

commaOperator: stringConjunction (BR* "," BR* stringConjunction)*

stringConjunction: ternary (BR* "|" BR* ternary)*

ternary: in (BR* "?" BR* ternary ":" ternary)?

in: or (BR* "in" BR* or)*

or: and (BR* "||" BR* and)*
and: eq (BR* "&&" BR* eq)*
eq: comp (BR* ("=="|"!=") BR* comp)*
comp: range (BR* (">"|"<"|">="|"<=") BR* range)*

range: random (BR* ".." BR* random)*
random: term (BR* "~" BR* term)*

term: factor (BR* ("+"|"-") BR* factor)*
factor: unaryPrefix (BR* ("*"|"/") BR* unaryPrefix)*

unaryPrefix: (("!"|"-"|"debug") BR*)* unaryPostfix
unaryPostfix: access ("(" BR* expr BR* ")")*

access: primaryExpr ( BR* "." BR* IDENTIFIER )*

primaryExpr
	: "(" BR* expr BR* ")"
	| IDENTIFIER (BR* block)?
	| ( "(" param ")" | param ) "->" ( block | expr )
	| block
	| literal

literal
	: "true"
	| "false"
	| NUMBER
	| NAMESPACE
	| STRING


param: (BR* IDENTIFIER BR* ",")* BR*

# stupid lexical grammars, here you go

NUMBER: DIGIT+ ("." DIGIT+)?
NAMESPACE
	: "<" ("a"..."z" | "0"..."9" | "_" | "-" | ".")+
	( ":" ("a"..."z" | "0"..."9" | "_" | "-" | "." | "/")+)? ">"
STRING
	: "\"" STRING_CONTENT "\""
	| "'" STRING_CONTENT "'"

STRING_CONTENT
	: "\\" ( "t" | "n" | "\\" | "\"" | "'" | "u" HEX{4} )
	| ^"\\\r\n\""

IDENTIFIER: ALPHABET (ALPHABET|DIGIT)*

DIGIT: "0"..."9"

ALPHABET: "a"..."z" | "A"..."Z" | "_"
HEX: DIGIT|"A"..."F"|"a"..."f"

BR: "\r"|"\n"